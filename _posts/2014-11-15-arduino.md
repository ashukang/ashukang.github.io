---
layout: post
title: "Arduino+超声波测距做控制器"
description: "利用Arduino nano+超声波传感器来做一个简单的拍手手势识别器，从而实现一个拍手控制器"
category: "smart"
tags: ["arduino"]
---
{% include JB/setup %}

Arduino是非常容易上手的电子积木，一个小巧的控制核心再加上一些功能拓展模块，就可以做出一个个有趣的玩意儿。今天想分享的是超声波距离传感器的应用，利用超声波距离传感器可以测量出障碍物的距离，利用这一个特性，我们可以通过距离差来识别拍手的手势，从而做到识别拍手这个动作，让这个组合实现一个控制器的功能。


 - 项目的源代码[ github ][1]
 - 示例视频

<div align="center"><embed src="http://player.youku.com/player.php/sid/XODI3MzY2MzU2/v.swf" allowFullScreen="true" quality="high" width="480" height="400" align="middle" allowScriptAccess="always" type="application/x-shockwave-flash" /></div>

实验器材
--------

 - Arduino nano + 拓展板

<div align="center"><img src="http://i2.tietuku.com/61a359d85efab6d2.jpg" alpngt="Arduino nano"/></div>

 - HC-SR04 超声波传感器

<div align="center"><img src="http://i2.tietuku.com/97fad545c014e502.jpg" alpngt="HC-SR04"/></div>

 - 面包板 + 杜邦线

<div align="center"><img src="http://i2.tietuku.com/33cee384cdc39e28.jpg" alpngt="面包板"/></div>


实现超声波测距
--------------

 - 下载 [ NewPing ][2] 库，并安装到 Arduino 的插件中。
 - 新建 Arduino 项目。

>写入示例代码。

	#include <NewPing.h>

	#define TRIGGER_PIN  	2
	#define ECHO_PIN     	3
	#define MAX_DISTANCE 	200
	#define DELAY_TIME 	50

	NewPing sonar(TRIGGER_PIN, ECHO_PIN, MAX_DISTANCE);

	int uS;
	int curCM;

	void setup() {
  		Serial.begin(115200);
		curCM = 0;
  		uS = 0;
	}

	void loop() {
  		delay(DELAY_TIME);
  		uS = sonar.ping();
  		curCM = uS / US_ROUNDTRIP_CM;
    		Serial.print("Ping: ");
    		Serial.print(curCM);
    		Serial.println(" cm");
  	}


 - 将 Arduino nano 和 HC-SR04 超声波传感器相连。

>VCC接5v，GND相接，根据示例代码的配置，将Trig接D2，将Echo接D3，实际上Trig与Echo不一定都要接到D2、D3处，只要和代码写定的阵脚对应即可。为了方便操作我使用面包板来连接，并找来个简单的支架固定面包板，确保超声波传感器的正面朝上，这样更加方便测试。

<div align="center"><img src="http://i2.tietuku.com/a4ba73586f2c103b.jpg" alpngt="连线"/></div>

>将 Arduino nano 接到 PC，然后将示例代码编译上传到 Arduino nano 板上，就可以看到有实际的距离打印出来了，可以将手掌盖着超声波传感器上方，并改变上下的位置，观察距离测量值的变化。

实现拍手识别
------------

 - 找到拍手引起距离变化的规律

>以正常的拍手动作，在超声波传感器上方以一定的频率执行，这样更加容易找到想要的判断条件。为了更好的处理数据，可以修改下 loop() 方法，输出纯数据的原始值。

	void loop() {
  		delay(DELAY_TIME);
  		uS = sonar.ping();
  		curCM = uS / US_ROUNDTRIP_CM;
    		Serial.println(curCM);
  	}

>操作10次后，将原始数据拷贝到 Excel 中，然后做成一个图表，图表能帮助我们快速地在一堆数据中找到规律。

<div align="center"><img src="http://i2.tietuku.com/d0819f005dfca53c.png" alpngt="拟合"/></div>

>适当调整显示比例以及刻度值，可以发现有很多明显的波谷，很显然这些波谷对应的时刻，正是拍手时手掌处于最低处的时刻，观察到红色箭头的位置，基本上所有波谷开始的时候都是会穿过这条线，因此可以得到一个粗略的判断条件，就是如果两次距离测量值的连线是穿过10cm的，则认为执行了一次拍手操作。

	include <NewPing.h>

	#define TRIGGER_PIN  		2
	#define ECHO_PIN     		3
	#define MAX_DISTANCE 		200
	#define DELAY_TIME 		50
	#define THRESHOLD_DISTANCE 	10

	NewPing sonar(TRIGGER_PIN, ECHO_PIN, MAX_DISTANCE);

	int uS;
	int curCM;
	int lastCM;
	int hitCount;

	void setup() {
  		Serial.begin(115200);
  		// init
  		curCM = 0;
  		lastCM = 0;
		hitCount = 0;
  		uS = 0;
	}

	void loop() {
  		delay(DELAY_TIME);
  		uS = sonar.ping();
  		curCM = uS / US_ROUNDTRIP_CM;
  		if (lastCM > THRESHOLD_DISTANCE  
    			&& curCM > 0 && curCM <= THRESHOLD_DISTANCE) {
    			hitCount++;
    			Serial.print("Ping: ");
    			Serial.print(curCM);
    			Serial.print(" cm, hit ");
    			Serial.println(hitCount);
  		}
  		Serial.println(curCM);
  		lastCM = curCM;
	}

>重新编译上传控制程序，可以发现确实能识别拍手的手势，但是却存在一个问题，就是能看到明显地发现在一个拍手的过程中出现了两次触发动作。因为我们很难保证每次拍手时都能落在有效的区域内，同时每次拍手的频率固定。

<div align="center"><img src="http://i2.tietuku.com/737cef381dc47f74.png" alpngt="拟合"/></div>

>所以这种单一的判断条件不能很好的满足要求，我们假设以正常的速度拍手，既不会出现特别高的频率，那么就可以限定一个有效的时间阈值，在这个时间阈值内如果出现多次的拍手识别，就当作是误判，只算一次。这里假定这个时间阈值为200ms。

	include <NewPing.h>

	#define TRIGGER_PIN  		2
	#define ECHO_PIN     		3
	#define MAX_DISTANCE 		200
	#define DELAY_TIME 		50
	#define THRESHOLD_DISTANCE 	10
	#define VALID_TIME_INTERVAL 	200/DELAY_TIME

	NewPing sonar(TRIGGER_PIN, ECHO_PIN, MAX_DISTANCE);

	int uS;
	int curCM;
	int lastCM;
	int hitCount;
	int lastHitStamp;

	void setup() {
  		Serial.begin(115200);
  		// init
  		curCM = 0;
  		lastCM = 0;
  		hitCount = 0;
  		lastHitStamp = 0;
  		uS = 0;
	}

	void loop() {
  		delay(DELAY_TIME);
  		lastHitStamp++;
  		uS = sonar.ping();
  		curCM = uS / US_ROUNDTRIP_CM;
  		if (lastCM > THRESHOLD_DISTANCE  
    			&& curCM > 0 && curCM <= THRESHOLD_DISTANCE 
    			&& lastHitStamp >= VALID_TIME_INTERVAL) { 

    			hitCount++;
    			lastHitStamp = 0;
    			Serial.print("Ping: ");
    			Serial.print(curCM);
    			Serial.print(" cm, hit ");
    			Serial.println(hitCount);
  		}
  		Serial.println(curCM);
  		lastCM = curCM;
	}

>现在已经没有出现误报的情况了，但是还有一个问题，如果现在将手掌处于10cm处，既处于位置阈值的地发，那么稍稍地上下移动也是会被识别为拍手，这个与我们的预期有点出入，我们希望能有一个上下挥动的动作，出现这个问题的根源是因为我们的这个判断条件太过简单了，并没有考虑到两个测量位置之间的斜率，很明显需要有一个较大的倾斜斜率才能体现出上下挥动的动作。假定两个测量位置之间的时间差是单位时间，既两个测量位置之间的斜率可以转换为两个测量位置之间的差值。假定两个测量位置之间的差值必须大于2cm。

	#define VALID_DISTANCE 2

	void loop() {
  		delay(DELAY_TIME);
  		lastHitStamp++;
  		uS = sonar.ping();
  		curCM = uS / US_ROUNDTRIP_CM;
  		if (lastCM > THRESHOLD_DISTANCE  
    			&& curCM > 0 && curCM <= THRESHOLD_DISTANCE 
    			&& lastHitStamp >= VALID_TIME_INTERVAL
    			&& (lastCM - curCM) >= VALID_DISTANCE) {

    			hitCount++;
    			lastHitStamp = 0;
    			Serial.print("Ping: ");
    			Serial.print(curCM);
    			Serial.print(" cm, hit ");
    			Serial.println(hitCount);
  		}
  		Serial.println(curCM);
  		lastCM = curCM;
	}

>现在的判断条件基本满足要求，但是再加快一下拍手的速度，会发现有些动作是会被漏识别的。为什么会出现这种情况呢？回看我们一开始模拟出来的测量位置连线图，实际上我们得到的只是一些点，既位置，而那些连线是我们为了方便判断，虚拟出来的，用离散的数值来模拟连续的过程，是计算机中惯用的伎俩。很明显，如果连续过程变化得很快，但是离散化采样的时间间隔又非常的长，那么势必会丢掉部分的信息，将一些细节丢失了。就目前我们使用的实验器材来看，离散化的处理方式我们是无法更改的，但是采样率是可以调整，可以通过提高采样率来保留更多的信息，很明显的是这个采样率的提高幅度不会是无限的，否则这个过程又会被抽象成来一个连续的。所以在使用这个控制器时，也是需要人为的限定下拍手时上下挥动的速度，以保证是在此控制器的量程范围内。将采样率提高为原来的两倍，基本满足稍快手速的要求。

	#define DELAY_TIME 25

>到此，就实现了一个利用 Arduino nano 和超声波传感器来识别上下挥手动作的控制器，每当上下挥动一次手掌时，会发出一个识别的事件。当然这只是一个非常基础的模型，其识别条件也不是最完美的，但是相信我们已经为下一步好好利用它打下了基础，或许能做出来更加有趣的东西。

  [1]: https://github.com/ashukang/arduino-demo/tree/master/HC_SR04_2
  [2]: http://code.google.com/p/arduino-new-ping
